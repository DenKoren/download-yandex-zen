#!/usr/local/bin/fish
# vim: set noexpandtab:

function prompt_text
	set --local prompt $argv[1]

	osascript -e "
		set userInput to display dialog \"$prompt\" default answer \"\" buttons {\"Cancel\", \"Continue\"} default button \"Continue\"

		if button returned of userInput is \"Continue\" then
			text returned of userInput
		else
			\"\"
		end if
	"
end

function prompt_file
	set --local prompt $argv[1]

	osascript -e "
		set downloadFile to choose file name with prompt \"$prompt\"
		POSIX path of downloadFile
	"
end

function alert_user
	set --local alert_heading $argv[1]
	set --local alert_body $argv[2]

	osascript -e "
		set a to display dialog \"$alert_heading\n\n$alert_body\" buttons {\"Exit\"} default button \"Exit\" giving up after 10
	" >/dev/null
	exit 1
end

set --local url (prompt_text "Please, provide URL to the page with video")
if [ -z $url ]
	alert_user "Video URL is empty" "Have no idea what to download."
end

set --local file_path (prompt_file "Choose where to save the downloaded video.\nTo download only audio, use '.mp3' or '.wav' file extension")
if [ -z $file_path ]
	exit 1
end

if [ -f $file_path ]
	rm $file_path
end

/usr/local/bin/download-yandex-zen $url $file_path
